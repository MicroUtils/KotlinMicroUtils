buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }

    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.io.gitlab.arturbosch:detekt-gradle-plugin:latest.release"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1"
    }
}

group 'MicroUtils'
version '1.3'



apply plugin: 'kotlin'
// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'io.github.microutils:kotlin-logging:1.5.4'

    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    testCompile 'junit:junit:4.12'
    testCompile 'io.kotlintest:kotlintest-runner-junit4:3.1.6'
}

//https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = 'oshai'
    key = 'my_key' //https://bintray.com/profile/edit
    publications = ['MyPublication']
    pkg {
        repo = 'KotlinMicroUtils'
        name = 'KotlinMicroUtils'
        userOrg = 'microutils'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/MicroUtils/KotlinMicroUtils'
        version {
            name = "$version"
            desc = "KotlinMicroUtils $version"
            released = new Date()
        }
    }
}
// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'microutils'
            artifactId 'KotlinMicroUtils'
            version "$version"

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

detektCheck.dependsOn(test)
check.dependsOn(detektCheck)